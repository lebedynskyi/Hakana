apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = code
        }
        return Integer.parseInt(code.toString().trim())
    }
    catch (ignored) {
        return 100500;
    }
}

def getVersionName = { ->
    try {
        def code = getVersionCode()
        return String.valueOf(code / 100)
    }
    catch (ignored) {
        return 'Unknown';
    }
}

android {
    compileSdkVersion 18
    buildToolsVersion "21.0.1"

    defaultConfig {
        applicationId "net.japan.kana.hakana"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode getVersionCode()
        versionName getVersionName()
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.google.android.gms:play-services:6.1.+'
    compile 'com.jakewharton:butterknife:6.0.0'
}
